.386
;******************************************
;*	   K  L  C  -  D  E  M	O	  *
;*  N O   N A M E   C R E W		  *
;*    CODE   : PIOTR GOLASZEWSKI - R E X  *
;*  GRAPHICS : JAN KALISZEWSKI	 - Z U O  *
;*    MUSIC  : JAN KALISZEWSKI	 - Z U O  *
;*    JOKES  : R E X  &  Z U O		  *
;*     WARSAW  1994			  *
;******************************************

STACK SEGMENT BYTE STACK USE16
	DB 2048 DUP (?)
STACK ENDS

DATA SEGMENT BYTE PUBLIC USE16
	F DB '..\files\FONT.DAT',0
	FONT  DB 60416 DUP (11) ;*** WZORY FONTOW OD 32 DO 90 WL.
		TEKST DB  'HEY YOU THERE    REXER  @ ' ;*** TEKST
		TEKST2 DB 'WHAT ? WHEN ? WHY ? ..    '
	WPL   DW 0 ;*** POZ W TEKS AKT. UZUPELNIANEJ LITERY ( 0.. );
	WLL   DW 8 ;*** NR.NASTEPNEJ LINII DO UZUPELNIENIA Z LITERY NA EKRAN.
	ADZN  DW ? ;*** PRZES.(W FONT) DO AKTUALNIE WYSWIETLANEGO ZNAKU NA EKR.
	ADZN2 DW ?
DATA ENDS

CODE SEGMENT BYTE PUBLIC USE16
	ASSUME CS:CODE,DS:DATA,SS:STACK
START:
		PUSH DATA
		POP DS

	MOV DX,OFFSET F ;*** OTWORZ FONT.DAT
	MOV AL,0
	MOV AH,3DH
	INT 21H
	MOV BX,AX

		MOV AH,3FH  ;*** ODCZYTAJ 32*32*59 BAJTOW W KODZIE POZIOMYM
	MOV CX,60416
	MOV DX,OFFSET FONT
	INT 21H

	MOV AH,3EH  ;*** ZAMKNIJ PLIK FONT.DAT
	INT 21H

	MOV AX,13H ;*** TRYB GRAFICZNY (#) (320X200X256C)
	INT 10H


	MOV CX,1
	MOV DX,3C8H
	MOV	BX,0
L000:
	MOV AL,CL
	OUT DX,AL
	INC DX

	MOV AL,BL
	OUT DX,AL

	MOV AL,0
	OUT DX,AL

	MOV AL,BL
	OUT DX,AL

	DEC DX
	ADD BX,5
	INC CX
	CMP CX,256
	JB  L000

;*** PETLA PROG :
PETLA:	CALL SCROLL ;*** WOLAMY PROCEDURY KROKOW ANIMACJI

	IN  AL,60H  ;  **** CZY ESC ?
	CMP AL,1
	JNE L004

	STI	;  ***	TAK WYCHODZ Z PROGRAMU !!!
	MOV AX,0003H
	INT 10H
	MOV AX,4C00H
	INT 21H

L004:	MOV BX,WPL
	CMP TEKST[BX],64
JNZ PETLA    ;*** GDY JESZCZE NIE WSZYSTKIE LITERY WYSWIETLILISMY I KONIEC DEMA.

;*** LADNE ZAKONCZENIE DEMA :





JMP KONIEC

;***********************
;*	PROCEDURY :    *
;***********************

SCROLL PROC
	mov	dx,03dah
@@33:
	in	al,dx
	test	al,08h
	jz     @@33
@@44:
	in	al,dx
	test	al,08h
	jnz	 @@44
	CMP WLL,32
	JNZ SKOK
	MOV WLL,0 ;*** TUTAJ GDY CHCEMY NOWA LITERA
	MOV BX,WPL
	MOV DL,TEKST[BX]
	SUB DL,32
MOV CL,TEKST2[BX]
SUB CL,32
	MOV DH,0
MOV CH,0
	MOV AX,1024
	MUL DX
	MOV ADZN,AX  ;*** PRZESUNIE CIE W FONT NOWEJ LITERY
MOV AX,1024
MUL CX
MOV ADZN2,AX
	INC WPL
 SKOK:	MOV AX,0A000H ;*** SCROLLING EKRANU (#)(SVGA-A0000H)
	MOV ES,AX
	MOV DI,320 ;*** PRZES. DO OSTATNIEJ LINII (#)
	MOV SI,0 ;*** PRZES. DO PRZED OST LINII (#)
	MOV CX,199 ; ILE LINI PRZENOSZE (MAXY-1) (#)
P1: MOV BX,0
 P2:	MOV AL,ES:[DI][BX]
		MOV ES:[SI][BX],AL
MOV AL,ES:[DI+288][BX]
MOV ES:[SI+288][BX],AL
	INC BX
 CMP BX,32
 JNZ P2
		ADD DI,320 ; NASTEPNA LINIA DO PRZENIESIENIA (#)
	ADD SI,320
LOOP P1
	MOV BX,0 ;*** NOWA LINIJKA Z FONT U DOLU EKRANU
	MOV DI,ADZN
MOV SI,ADZN2
	MOV DX,WLL
	MOV AX,32
	MUL DX
	ADD DI,AX
ADD SI,AX
P3: MOV AL,FONT[DI][BX]
	MOV ES:[63680][BX],AL ;*** 63..--ARES DO OSTATNIEJ LINII (#)
MOV AL,FONT[SI][BX]
MOV ES:[63968][BX],AL ;*** 63..--ARES DO OSTATNIEJ LINII (#)
	INC BX
CMP BX,32
JNZ P3
	INC WLL
RET
SCROLL ENDP



KONIEC: MOV AH,0
		MOV AL,03H
		INT 10H
		MOV AX,4C00H	;*** ZAKONCZENIE PROGRAMU
		INT 21H

CODE ENDS
	END START
